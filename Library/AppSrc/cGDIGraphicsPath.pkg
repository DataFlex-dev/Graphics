Use cGDIPlus.pkg
Use cGDIGraphicsPath.h.pkg
Use cGDIPen.pkg
Use tWinStructs.pkg
Use Structs\tPointF.pkg

{ ClassLibrary = Windows }
{ HelpTopic = cGDIGraphicsPath }
{ Description = "A GraphicsPath object stores a sequence of lines, curves, and shapes." }
{ Description += "You can draw the entire sequence by calling the DrawPath method of a Graphics Object." }
{ Description += "You can partition the sequence of lines, curves, and shapes into figures, and with the help of a GraphicsPathIterator Object, you can draw selected figures. You can also place markers in the sequence, so that you can draw selected portions of the path." }
Class cGDIGraphicsPath is a cObject
    { Description = "Creates a property to hold the Pointer to the path" }
    { MethodType = Event }
    { Visibility = Private }
    Procedure Construct_Object
        Forward Send Construct_Object

        { Visibility = Private }
        { Description = "Stores the pointer of a graphics path object" }
        Property Pointer ppPath
    End_Procedure

    { Description = "Destroys the path before the object is destroyed" }
    { MethodType = Event }
    { Visibility = Private }
    Procedure Destroy_Object
        Send DeletePath

        Forward Send Destroy_Object
    End_Procedure

    { Description = "Creates a GraphicsPath object and initializes the fill mode" }
    { Description += "Returns a non-zero value (the GDI+ result value) when the object could not be created." }
    { Description += "Handle this return value" }
    { MethodType = Method }
    Function CreatePath gpFillMode eMode Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Move 0 to pPath
        Move (GdipCreatePath (eMode, AddressOf (pPath))) to eStatus
        If (eStatus = gpOk) Begin
            Set ppPath to pPath
        End

        Function_Return eStatus
    End_Function

    { Description = "Creates a GraphicsPath object based on an array of points, an array of types, and a fill mode." }
    { Description += "Returns a non-zero value (the GDI+ result value) when the object could not be created." }
    { Description += "Handle this return value" }
    { MethodType = Method }
    Function CreatePath2 tPointF[] PathPoints Char[] PathTypes gpFillMode eMode Returns gpStatus
        gpStatus eStatus
        Pointer pPath
        Integer iCount

        Move 0 to pPath
        Move (SizeOfArray (PathPoints)) to iCount
        Move (GdipCreatePath2 (AddressOf (PathPoints), AddressOf (PathTypes), iCount, eMode, AddressOf (pPath))) to eStatus
        If (eStatus = gpOk) Begin
            Set ppPath to pPath
        End

        Function_Return eStatus
    End_Function

    { Description = "Creates a GraphicsPath object based on an array of points, an array of types, and a fill mode." }
    { Description += "Returns a non-zero value (the GDI+ result value) when the object could not be created." }
    { Description += "Handle this return value" }
    { MethodType = Method }
    Function CreatePath2I tWinPoint[] PathPoints Char[] PathTypes gpFillMode eMode Returns gpStatus
        gpStatus eStatus
        Integer iCount
        Pointer pPath

        Move 0 to pPath
        Move (SizeOfArray (PathPoints)) to iCount
        Move (GdipCreatePath2I (AddressOf (PathPoints), AddressOf (PathTypes), iCount, eMode, AddressOf (pPath))) to eStatus
        If (eStatus = gpOk) Begin
            Set ppPath to pPath
        End

        Function_Return eStatus
    End_Function

    { Description = "Creates a new GraphicsPath object, and initializes it with the contents of this GraphicsPath object." }
    { Description += "Returns a the DataFlex object handle when succesful." }
    { MethodType = Method }
    Function ClonePath Returns Handle
        gpStatus eStatus
        Pointer pSourcePath pClonedPath
        Handle hoClonedPath

        Get ppPath to pSourcePath
        If (pSourcePath <> 0) Begin
            Move 0 to pClonedPath
            Move (GdipClonePath (pSourcePath, AddressOf (pClonedPath))) to eStatus
            If (eStatus = gpOk) Begin
                Get CreateNamed of ghoGDIPlusHandler (RefClass (cGDIGraphicsPath)) "ClonedPath" to hoClonedPath
                Set ppPath of hoClonedPath to pClonedPath
            End
            Else Begin
                Send HandleGDIResult of ghoGDIPlusHandler eStatus 'ClonePath'
            End
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return hoClonedPath
    End_Function

    { Description = "Releases resources used by the GraphicsPath object" }
    { Description += "Sends HandleGDIResult when the GDI+ Function fails." }
    { MethodType = Method }
    Procedure DeletePath
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath) Begin
            Move (GdipDeletePath (pPath)) to eStatus
            If (eStatus = gpOk) Begin
                Set ppPath to 0
            End
            Else Begin
                Send HandleGDIResult of ghoGDIPlusHandler eStatus 'DeletePath'
            End
        End
    End_Procedure

    { Description = "Adds an elliptical arc to the current figure of this path." }
    { Description += "Returns a non-zero (GDI+ return) value when the elliptical arc could not be added." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { MethodType = Method }
    Function AddArc Float fX Float fY Float fWidth Float fHeight Float fStart Float fSweep Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipAddPathArc (pPath, fX, fY, fWidth, fHeight, fStart, fSweep)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Adds an elliptical arc to the current figure of this path." }
    { Description += "The difference between this function and the AddArc function is that the first four arguments are integers versus floating point values." }
    { Description += "Returns a non-zero (GDI+ result) value when the elliptical arc could not be added." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { MethodType = Method }
    Function AddArcI Integer iX Integer iY Integer iWidth Integer iHeight Float fStart Float fSweep Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipAddPathArcI (pPath, iX, iY, iWidth, iHeight, fStart, fSweep)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Adds a line to the current figure of this path." }
    { Description += "Returns a non-zero (GDI+ return) value when the line could not be added." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { Description += "Calls HandleGDIError when no GDI+ path object available." }
    { MethodType = Method }
    Function AddLine Float fFromX Float fFromY Float fToX Float fToY Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipAddPathLine (pPath, fFromX, fFromY, fToX, fToY)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Adds a line to the current figure of this path." }
    { Description += "Returns a non-zero (GDI+ return) value when the line could not be added." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { Description += "Calls HandleGDIError when no GDI+ path object available." }
    { MethodType = Method }
    Function AddLineI Integer iFromX Integer iFromY Integer iToX Integer iToY Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipAddPathLineI (pPath, iFromX, iFromY, iToX, iToY)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Adds a rectangle to this path." }
    { Description += "The x, y, width, and height parameters in the flat function specify a rectangle that corresponds to the rect parameter in the wrapper method." }
    { Description += "Returns a non-zero (GDI+ result) value when the rectangle could not be added." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { MethodType = Method }
    Function AddRect Float fX Float fY Float fWidth Float fHeight Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipAddPathRectangle (pPath, fX, fY, fWidth, fHeight)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Adds a rectangle to this path." }
    { Description += "The x, y, width, and height parameters in the flat function specify a rectangle that corresponds to the rect parameter in the wrapper method." }
    { Description += "Returns a non-zero (GDI+ result) value when the rectangle could not be added." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { MethodType = Method }
    Function AddRectI Integer iX Integer iY Integer iWidth Integer iHeight Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipAddPathRectangleI (pPath, iX, iY, iWidth, iHeight)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Closes the current figure of this path." }
    { Description += "Returns a non-zero (GDI+ return) value when the current figure could not be closed." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { MethodType = Method }
    Function CloseFigure Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipClosePathFigure (pPath)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Closes all open figures in this path." }
    { Description += "Returns a non-zero (GDI+ return) value when the figures could not be closed." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { MethodType = Method }
    Function CloseAllFigures Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipClosePathFigures (pPath)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Gets the number of points in this path's array of data points." }
    { Description += "This is the same as the number of types in the path's array of point types" }
    { Description += "Calls HandleGDIError when the Font or StringFormat object are not available." }
    { Description += "Sends HandleGDIResult if the GDI+ GetFontUnit function fails" }
    { MethodType = Method }
    Function PointCount Returns Integer
        gpStatus eStatus
        Pointer pPath
        Integer iCount

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move 0 to iCount
            Move (GdipGetPointCount (pPath, AddressOf (iCount))) to eStatus
            If (eStatus <> gpOk) Begin
                Send HandleGDIResult of ghoGDIPlusHandler eStatus "PointCount"
            End
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return iCount
    End_Function
    
    { Description = "Gets this path's array of point types" }
    { Description += "A GraphicsPath object has an array of points and an array of types." }
    { Description += "Each element in the array of types is a byte that specifies the point type and a set of flags for the corresponding element in the array of points. Possible point types and flags are listed in the PathPointType enumeration." }
    { MethodType = Method }
    Function PathTypes Returns UChar[]
        gpStatus eStatus
        Pointer pPath
        Integer iCount
        UChar[] ucData

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move 0 to iCount
            Move (GdipGetPointCount (pPath, AddressOf (iCount))) to eStatus
            If (eStatus = gpOk) Begin
                Move (ResizeArray (ucData, iCount)) to ucData
                Move (GdipGetPathTypes (pPath, AddressOf (ucData), iCount)) to eStatus
                If (eStatus <> gpOk) Begin
                    Send HandleGDIResult of ghoGDIPlusHandler eStatus "PathTypes"
                End
            End
            Else Begin
                Send HandleGDIResult of ghoGDIPlusHandler eStatus "PathTypes:PointCount"
            End
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return ucData
    End_Function
    
    { Description = "Replaces this path with curves that enclose the area that is filled when this path is drawn by a specified pen. This method also flattens the path." }
    { Description += "Returns a non-zero (GDI+ return) value when the path could not be replaced." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path or no pen object" }
    { MethodType = Method }
    Function PathWiden Handle hoPen Handle hoMatrix Float nFlatness Returns gpStatus
        Boolean bIsPen bIsMatrix
        gpStatus eStatus
        Pointer pPath pMatrix pPen

        If (hoPen <> 0) Begin
            Get IsObjectOfClass of hoPen (RefClass (cGDIPen)) to bIsPen
            If (bIsPen) Begin
                Get ppPen of hoPen to pPen
                If (pPen <> 0) Begin
                    Get ppPath to pPath
                    If (pPath <> 0) Begin
                        If (hoMatrix <> 0) Begin
                            Get IsObjectOfClass of hoMatrix (RefClass (cGDIMatrix)) to bIsMatrix
                            If (bIsMatrix) Begin
                                Get ppMatrix of hoMatrix to pMatrix
                            End
                            Else Begin
                                Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_MatrixObjectNotAvailable
                                Move 0 to pMatrix
                            End
                        End
                        Else Begin
                            Move 0 to pMatrix
                        End
                        Move (GdipWidenPath (pPath, pPen, pMatrix, nFlatness)) to eStatus
                    End
                    Else Begin
                        Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
                    End
                End
                Else Begin
                    Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_PenObjectNotAvailable
                End
            End
            Else Begin
                Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_PenObjectNotAvailable
            End
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_PenObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Empties the path and sets the fill mode to FillModeAlternate" }
    { Description += "Returns a non-zero (GDI+ return) value when the current figure could not be reset." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { MethodType = Method }
    Function ResetPath Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipResetPath (pPath)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function

    { Description = "Starts a new figure without closing the current figure." }
    { Description += "Subsequent points added to this path are added to the new figure." }
    { Description += "Returns a non-zero (GDI+ return) value when the current figure could not be closed." }
    { Description += "Handle this status If needed." }
    { Description += "The function calls HandleGDIError when there is no graphics path object" }
    { MethodType = Method }
    Function StartFigure Returns gpStatus
        gpStatus eStatus
        Pointer pPath

        Get ppPath to pPath
        If (pPath <> 0) Begin
            Move (GdipStartPathFigure (pPath)) to eStatus
        End
        Else Begin
            Send HandleGDIError of ghoGDIPlusHandler C_GDIErr_GraphicsPathObjectNotAvailable
        End

        Function_Return eStatus
    End_Function
End_Class
